using for loops

on join:
	unlock custom recipes with id "end_expansion" for player

on load:
	register smithing transform recipe:
		id: "end_expansion:dragon_slayer_sword"
		result: get_item("dragon_slayer_sword")
		template: get_item("end_upgrade_smithing_template")
		base: netherite sword
		addition: get_item("dragon_scale")

	set {_armor::helmet} to netherite helmet
	set {_armor::chestplate} to netherite chestplate
	set {_armor::leggings} to netherite leggings
	set {_armor::boots} to netherite boots
	for {_armor_type}, {_piece} in {_armor::*}:
		register smithing transform recipe:
			id: "end_expansion:enderite_%{_armor_type}%"
			result: get_item("enderite_%{_armor_type}%")
			template: get_item("end_upgrade_smithing_template")
			base: {_piece}
			addition: get_item("enderite_ingot")

	register shaped recipe:
		id: "end_expansion:warp_stone"
		result: get_item("warp_stone")
		shape: "cec", "apa", "oso"
		ingredients:
			set ingredient of "c" to chorus fruit
			set ingredient of "e" to ender eye
			set ingredient of "a" to amethyst shard
			set ingredient of "p" to ender pearl
			set ingredient of "o" to obsidian
			set ingredient of "s" to echo shard

	register new furnace recipe:
		id: "end_expansion:enderite_ingot"
		result: get_item("enderite_ingot")
		input: get_item("raw_enderite")
		cooktime: 30 seconds
		experience: 10
		category: "misc"

	register new shaped recipe:
		id: "end_expansion:end_upgrade_smithing_template"
		result: get_item("end_upgrade_smithing_template")
		shape: "sts", "ses", "sss"
		category: "equipment"
		ingredients:
			set ingredient of "s" to netherite scrap
			set ingredient of "t" to netherite upgrade smithing template
			set ingredient of "e" to get_item("enderite_ingot")
