on break of end stone:
	chance of 5%:
		send "&bRARE DROP! &eRaw Enderite &e(5%%)" to player
		set {_e::*} to give or return {endgive::raw_enderite} to player
		if {_e::*} is set:
			drop {_e::*} at player without velocity
			send "&cInventory full! Item dropped at your feet!"

on death of ender dragon:
	chance of 5%:
		drop 1 of {endgive::dragon_slayer_upgrade} at position of victim
	chance of 5%:
		drop 1 of {endgive::dragon_scale} at position of victim


on damage:
	attacker is a player
	victim is enderman or ender dragon or endermite
	if attacker's held item is {endgive::dragon_slayer_sword}:
		set damage to damage * 1.1

command /endgive <player> <text>:
	permission: endexpansion.admin
	trigger:
		set {_itemlist::*} to (indexes of {endgive::*})
		if {_itemlist::*} does not contain arg-2:
			send "&cInvalid item."
			stop

		set {_item} to {endgive::%arg-2%}
		give {_item} to arg-1
		send "&aGiven %name of {_item}%&a to %arg-1%."

on tab complete of "endgive":
	set tab completions for position 2 to indexes of {endgive::*}