on right click:
	if get_id(tool of player) != "warp_stone":
		stop
	cancel event
	activate_warp_stone(player)

function get_warp_position(p: player) :: location:
	return {end_expansion::warp_position::%{_p}%}

function set_warp_position(p: player, pos: location):
	set {end_expansion::warp_position::%{_p}%} to location of {_p}

function activate_warp_stone(p: player):
	set {_warp} to get_warp_position({_p})
	if {_p} is not sneaking:
		stop if precondition({_p}, whether {_warp} is not set, "&7Set a location before teleporting") = true
		set {_since} to time since {-warp_cooldown::%{_p}%}
		stop if precondition({_p}, whether {_since} < 5 minutes, "&eWarp on cooldown &c%{_since}%/5 minutes") = true
		set {-warp_cooldown::%{_p}%} to now
		teleport {_p} to {_warp}
		delete {end_expansion::warp_position::%{_p}%}
		set tool of {_p} to air

	else:
		stop if precondition({_p}, whether ({_warp} is set), "&7Your location is already set") = true

		set {_pos} to location of {_p}

		set_warp_position({_p}, {_pos})
		set {_location} to get_pretty_location({_pos})
		send "&7Location set to %{_location}%" to {_p}
		set line 6 of lore of (tool of {_p}) to "&7Location: %{_location}%"
